x y z ;;

# c1
lambda s. lambda z. s z ;;

# successeur de z√©ro
(lambda n. lambda s. lambda z. s (n s z)) lambda s. lambda z. z ;;

# un terme non clos
val bug = lambda x. (lambda y. x y) y ;;

# un autre
s ((lambda s. lambda z. z) s z) ;;

# 1 + 2
(lambda m. lambda n. lambda s. lambda z. m s (n s z))
   (lambda s. lambda z. s z) (lambda s. lambda z. s (s z)) ;;

val tru = lambda t. lambda f. t ;;

val fls = lambda t. lambda f. f ;; 

val and = lambda b1. lambda b2. b1 b2 fls;;

val cond = lambda b. lambda t. lambda f. b t f ;;

val pair = lambda f. lambda s. lambda b. b f s ;;

val fst = lambda p. p tru ;;

val snd = lambda p. p fls ;;

val c0 = lambda s. lambda z. z ;;

val c1 = lambda s. lambda z. s z ;;

val c2 = lambda s. lambda z. s (s z) ;;

val c3 = lambda s. lambda z. s (s (s z)) ;;

val scc = lambda n. lambda s. lambda z. s (n s z) ;;

val iszro = lambda m. m (lambda x. fls) tru ;;

val plus = lambda m. lambda n. lambda s. lambda z. m s (n s z) ;;

val times = lambda m. lambda n. m (plus n) c0 ;;

val zz = pair c0 c0 ;;

val ss = lambda p. pair (snd p) (plus (scc c0) (snd p)) ;;

val prd = lambda m. fst (m ss zz) ;;

val equal = lambda m. lambda n. and (iszro (m prd n)) (iszro (n prd m)) ;;

equal (scc c0) c1 ;;

equal (plus c1 c2 ) c3 ;;

# fact 3
val f = lambda fct. lambda n. 
	cond (iszro n) (lambda x. c1) (lambda x. times n (fct (prd n))) c0 ;;

val z = lambda f. lambda y. (lambda x. f (lambda y. x x y)) (lambda x. f (lambda y. x x y)) y ;;

val fact = z f ;;

equal (fact c3) (times c2 c3) ;;

   